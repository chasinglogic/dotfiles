#!/usr/bin/env bb
; vi: ft=clojure

(require '[cheshire.core :as json]
         '[clojure.string :as str])

;; --- ANSI Color Codes ---
(def colors
  {:reset   "\u001b[0m"
   :red     "\u001b[31m"
   :yellow  "\u001b[33m"
   :cyan    "\u001b[36m"
   :bold    "\u001b[1m"
   :underline "\u001b[4m"})

(def color-for-level
  (memoize
   (fn [level]
     (case (str/upper-case (name level))
       ("INFO") (:cyan colors)
       ("WARN" "WARNING") (:yellow colors)
       ("ERR" "ERROR") (:red colors)
       (:reset colors)))))

;; --- State for dynamic keys ---
(defonce timestamp-key (atom nil))
(defonce level-key (atom nil))
(defonce message-key (atom nil))

(defn- discover-key [key-atom possible-keys data]
  (when-let [found-key (some #(when (contains? data %) %) possible-keys)]
    (reset! key-atom found-key)))

(defn- discover-keys [data]
  (when (nil? @timestamp-key)
    (discover-key timestamp-key ["time" "timestamp" "ts"] data))
  (when (nil? @level-key)
    (discover-key level-key ["levelname" "level_name" "level"] data))
  (when (nil? @message-key)
    (discover-key message-key ["message" "msg"] data)))

(declare print-fields)

(defn- print-nested [prefix key value nesting-level exclude-list]
  (println (str prefix key " = {"))
  (print-fields value (inc nesting-level) exclude-list)
  (println (str prefix "}")))

(defn- print-fields [data nesting-level exclude-list]
  (let [prefix (apply str (repeat nesting-level "\t"))]
    (doseq [[key val] data]
      (when-not (contains? exclude-list key)
        (cond
          (map? val) (print-nested prefix key val nesting-level exclude-list)
          (vector? val) (print-nested prefix key (into {} (map-indexed vector val)) nesting-level exclude-list)
          :else (println (str prefix key " = " val)))))))

(defn pretty-print [line]
  (let [data (json/parse-string line true)]
    (discover-keys data)

    (let [level (get data @level-key :INFO)
          timestamp (get data @timestamp-key "")
          msg (get data @message-key "")
          exclude-list (set (filter some? [@timestamp-key @level-key @message-key]))]

      (println (format "%s%s%s %s %s"
                       (color-for-level level)
                       (str/upper-case (name level))
                       (:reset colors)
                       timestamp
                       msg))
      (print-fields data 1 exclude-list))))

(defn main []
  (with-open [reader (java.io.BufferedReader. *in*)]
    (loop [line (.readLine reader)]
      (when line
        (try
          (pretty-print line)
          (catch Exception _e
            (println "NON-JSON LINE:" line)))
        (recur (.readLine reader))))))

(main)
